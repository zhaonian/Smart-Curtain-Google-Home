/*
 * Copyright 2019 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java'
  id 'war'
  id 'com.diffplug.gradle.spotless' version '3.27.1'
  id 'net.ltgt.errorprone' version '1.1.1'
  id 'org.gretty' version '3.0.1'
}

apply from: 'build-aws.gradle'
apply from: 'build-gcp.gradle'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

  implementation 'org.slf4j:slf4j-log4j12:1.7.28'
  implementation 'org.jetbrains:annotations:13.0'

  implementation 'com.google.actions:actions-on-google:1.8.0'
  implementation 'com.google.code.gson:gson:2.8.6'
  implementation 'com.google.protobuf:protobuf-java:3.10.0'
  implementation 'com.google.protobuf:protobuf-java-util:3.10.0'

  implementation 'com.google.firebase:firebase-admin:6.12.0'
  implementation 'com.google.cloud:google-cloud-firestore:1.31.0'
  implementation 'com.google.auth:google-auth-library-oauth2-http:0.18.0'
  implementation 'com.google.api:api-common:1.8.1'

// https://mvnrepository.com/artifact/org.eclipse.paho/org.eclipse.paho.client.mqttv3
  compile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.4'
// https://mvnrepository.com/artifact/org.processing/core
  compile group: 'org.processing', name: 'core', version: '3.3.6'
// https://mvnrepository.com/artifact/com.jayway.jsonpath/json-path
  compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
  testImplementation 'io.rest-assured:rest-assured:4.2.0'
  testImplementation 'org.hamcrest:hamcrest:2.2'

  errorprone 'com.google.errorprone:error_prone_core:2.3.4'
  errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

test {
  if (project.hasProperty('restassuredBaseUri')) {
     systemProperty 'restassuredBaseUri', project.restassuredBaseUri
  }
  useJUnitPlatform()
}

gretty {
  integrationTestTask = 'test'
  contextPath = '/'
}

spotless {
  java {
    googleJavaFormat()
    removeUnusedImports()
    importOrder 'java', 'javax', 'org', 'com'
  }
}

tasks.withType(JavaCompile).configureEach {
  options.errorprone {
    // TODO(proppy): Fix requires JDK 9+.
    disable('DoubleBraceInitialization')
    error('CatchAndPrintStackTrace')
    error('SystemExitOutsideMain')
    error('ModifiedButNotUsed')
    error('UnusedMethod')
    error('UnusedVariable')
  }
}
